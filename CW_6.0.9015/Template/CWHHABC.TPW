#!TEMPLATE (cwHHABC, 'HTML Help for Clarion'), FAMILY('ABC')
#!HELP('C60HELP.HLP')
#! -------------------------------------------------------------------------
#! (cwHH.TPL)
#! -------------------------------------------------------------------------
#!  VERSION 1.0
#! -------------------------------------------------------------------------
#! -------------------------------------------------------------------------
#EXTENSION (cwHHGlobal, 'Include HTML Help for Clarion Global Definitions'), DESCRIPTION('HTML Help Global Definitions'), APPLICATION(cwHHProc),HLP('~TPLHHGlobal')
#! -------------------------------------------------------------------------
#DISPLAY
#DISPLAY
#PROMPT( 'Default Help File Name: ',@s40),%cwHHgFileName
#DISPLAY( '(or variable prefixed with !)' )
#DISPLAY
#PROMPT( 'Append .HTM to Help IDs', CHECK ), %cwHHappendHTM, AT(10)
#DISPLAY
#! --------------------------------------------------------------------------
#ATSTART
#! --------------------------------------------------------------------------
#IF(~%Target32)
  #ERROR('HTML Help may only be used with 32 bit programs.')
#ELSE
  #IF( %CWVersion < '5002' )
    #ERROR( 'HTML Help has been Compile for Clarion version 5b' )
  #ELSIF( %CWVersion = '5002' )
    #PROJECT ('cwHH.LIB')
  #ELSE
    #IF( %ApplicationLocalLibrary )
      #PROJECT( 'cwHH%V%L.LIB' )
    #ELSE
      #PROJECT( 'cwHH%V%.LIB' )
    #ENDIF
  #END
#ENDIF
#ENDAT
#! --------------------------------------------------------------------------
#AT(%ShipList)
#! --------------------------------------------------------------------------
  #IF( NOT %ApplicationLocalLibrary )


    #IF( %CWVersion = '5002' )
___    cwHH.DLL - HTML Support DLL for Clarion 5.x
    #ELSE
___    cwHH60.DLL - HTML Support DLL for Clarion 6.0
    #END
  #ENDIF
#ENDAT
#! --------------------------------------------------------------------------
#AT(%BeforeGlobalIncludes)
#! --------------------------------------------------------------------------
  #IF( %ApplicationLocalLibrary )
   INCLUDE('cwHHL.INC'),ONCE
  #ELSE
   INCLUDE('cwHH.INC'),ONCE
  #ENDIF
#ENDAT
#! -------------------------------------------------------------------------
#! -------------------------------------------------------------------------
#!  VERSION 1.0
#! -------------------------------------------------------------------------
#! -------------------------------------------------------------------------
#EXTENSION (cwHHProc, 'Use HTML Help on a Procedure'), DESCRIPTION('Enable HTML Help on Procedure'), PROCEDURE, FIRST, REQ( cwHHGlobal ),HLP('~TPLHHPROC')
#! -------------------------------------------------------------------------
#BOXED('Help file')
  #DISPLAY( 'You may override the Global setting here.' )
  #PROMPT ( 'File Name: ',@s40), %cwHHFileName
#ENDBOXED
#BOXED('Context URL')
  #DISPLAY( 'The procedure HELP ID is used if it exists' )
  #DISPLAY( 'unless you override it here' )
  #PROMPT ( 'Context URL: ', @s40 ), %cwHHContextURL
#ENDBOXED
#BOXED('Trap key')
  #DISPLAY( 'You may override the Trap key (F12Key)' )
  #DISPLAY( 'NOTE: The "TRAP" key is NOT the Help key (F1Key)' )
  #DISPLAY( 'It is used as an ALIAS for the F1Key' )
  #PROMPT ( 'Alternate "TRAP" Key', @S20 ), %cwHHAltTrapKey
#ENDBOXED
#! --------------------------------------------------------------------------
#AT(%DataSection)
#! --------------------------------------------------------------------------
oHH           &tagHTMLHelp
#ENDAT
#! --------------------------------------------------------------------------
#AT(%WindowManagerMethodCodeSection,'Init','(),BYTE'),PRIORITY(9550)
#! --------------------------------------------------------------------------
#DECLARE( %hhInitString )
#DECLARE( %hhHelpTopic )
#IF( UPPER( CLIP( %cwHHAltTrapKey ) ) = 'F1KEY' )
  #DECLARE( %hhErrorKey )
  #ERROR( 'You cannot use F1 as the ALTERNATE trap key for HTML Help' )
  #SET(%hhErrorKey,'Please choose a different key for ' & %Procedure)
  #ERROR( %hhErrorKey )
#ENDIF
#SET( %hhInitString, '' )
#IF( CLIP(%cwHHFileName) = '' )
  #IF( SUB( %cwHHgFileName, 1, 1 ) = '!' )
    #SET( %hhInitString, SUB( %cwHHgFileName, 2, LEN( %cwHHgFileName ) ) )
  #ELSE
    #SET( %hhInitString, '''' & CLIP( %cwHHgFileName ) & '''' )
  #END
#ELSE
  #IF( SUB( %cwHHFileName, 1, 1 ) = '!' )
    #SET( %hhInitString, SUB( %cwHHFileName, 2, LEN( %cwHHFileName ) ) )
  #ELSE
    #SET( %hhInitString, '''' & CLIP( %cwHHFileName ) & '''' )
  #END
#ENDIF
#IF( CLIP( %cwHHAltTrapKey ) <> '' )
  #SET( %hhInitString, CLIP( %hhInitString ) & ',' & CLIP( %cwHHAltTrapKey ) )
#ENDIF
oHH &= NEW tagHTMLHelp
oHH.Init( %hhInitString )
#SET( %hhHelpTopic, EXTRACT(%WindowStatement,'HLP(',1) )
#IF( CLIP( %hhHelpTopic ) <> '' )
  #SET( %hhHelpTopic, CALL( %hhFixTopic, %hhHelpTopic ) )
oHH.SetTopic(%hhHelpTopic)
#ENDIF
#FOR(%Control)
  #SET( %hhHelpTopic, EXTRACT(%ControlStatement,'HLP(',1) )
  #IF( CLIP( %hhHelpTopic ) <> '' )
    #SET( %hhHelpTopic, CALL( %hhFixTopic, %hhHelpTopic ) )
oHH.SetTopic( %Control, %hhHelpTopic )
  #ENDIF
#ENDFOR
#ENDAT
#! --------------------------------------------------------------------------
#AT(%WindowManagerMethodCodeSection,'Kill','(),BYTE'),PRIORITY(9550)
#! --------------------------------------------------------------------------
IF ~oHH &= NULL
  oHH.Kill()
  DISPOSE( oHH )
END
#ENDAT
#! --------------------------------------------------------------------------
#! --------------------------------------------------------------------------
#GROUP( %hhFixTopic, %hhWorkString )
#! --------------------------------------------------------------------------
#! --------------------------------------------------------------------------
  #IF( SUB( %hhWorkString, 2, 1 ) = '~' )
    #SET( %hhWorkString, '''' & SLICE( %hhWorkString, 3, LEN( CLIP( %hhWorkString ) ) ) )
  #ENDIF
  #IF( %cwHHappendHTM )
    #SET( %hhWorkString, SLICE( %hhWorkString, 1, LEN( CLIP( %hhWorkString ) ) - 1 ) & '.htm''' )
  #END
  #RETURN( %hhWorkString )
#! --------------------------------------------------------------------------
#! --------------------------------------------------------------------------
#INCLUDE('cwHHCode.tpw')
