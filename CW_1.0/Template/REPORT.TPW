#PROCEDURE(Report,'Generic Reporting Procedure'),REPORT,PRIMARY('Report Procedure',OPTKEY),HLP('~TPLProcReport')
#LOCALDATA
RejectRecord         LONG,AUTO
LocalRequest         LONG,AUTO
LocalResponse        LONG,AUTO
WindowOpened         LONG,AUTO
RecordsToProcess     LONG,AUTO
RecordsProcessed     LONG,AUTO
RecordsPerCycle      LONG,AUTO
RecordsThisCycle     LONG,AUTO
PercentProgress      DECIMAL(4,1)
RecordStatus         BYTE,AUTO
#ENDLOCALDATA
#PROMPT('&Parameters:', @s255), %Parameters
#ENABLE(%ProcedureType='FUNCTION')
  #PROMPT('Return Value:',FIELD),%ReturnValue
#ENDENABLE
#PROMPT('Window Message:',@S40),%WindowMessage
#PROMPT('Print Preview',CHECK),%EnablePrintPreview
#PROMPT('Quick-Scan Records',CHECK),%EnableQuickScan,DEFAULT(1)
#BUTTON('Range and Filter'),AT(10,,180),HLP('~TPLProcReport')
  #PROMPT('&Record Filter:',@S255),%RecordFilter
  #ENABLE(%PrimaryKey)
    #PROMPT('Range Limit &Field:',COMPONENT(%PrimaryKey)),%RangeField
  #ENDENABLE
  #ENABLE(%RecordFilter OR %RangeField)
    #PROMPT('Approx. Record Count:',@N6),%ApproxRecordCount,REQ
  #ENDENABLE
  #ENABLE(%RangeField)
    #PROMPT('Range Limit &Type:',DROP('Current Value|Single Value|Range of Values')),%RangeLimitType,DEFAULT('Current Value')
    #BOXED('Range Limit Boundary:'),WHERE(%RangeLimitType='Single Value'),AT(,60)
      #PROMPT('&Range Limit Value:',FIELD),%RangeLimit
    #ENDBOXED
    #BOXED('Range Limit Boundaries:'),WHERE(%RangeLimitType='Range of Values'),AT(,60)
      #PROMPT('&Low Limit Value:',FIELD),%RangeLow
      #PROMPT('&High Limit Value:',FIELD),%RangeHigh
    #ENDBOXED
  #ENDENABLE
#ENDBUTTON
#INSERT(%ProcessInitializeProcedure)
#INSERT(%FileControlInitialize)
#CLASS('Procedure Setup','Upon Entry into the Procedure')
#CLASS('Before Lookups','After Record Retrieved, Before Lookups')
#CLASS('After Lookups','After Record Retrieved, After Lookups')
#CLASS('Procedure Exit','Before Leaving the Procedure')
#CLASS('Before Range Check','In Validate Record ROUTINE, Before Range Limit Code')
#CLASS('Before Filter Check','In Validate Record ROUTINE, Before Filter Code')
#CLASS('Before Print Detail','Before Printing Report Detail')
#EMBED(%GatherSymbols,'Gather Template Symbols'),HIDE
%Procedure %ProcedureType%Parameters
#INSERT(%ProcessDeclarations)
#INSERT(%StandardReportGeneration)
#INSERT(%StandardProgressWindow)
#IF(%EnablePrintPreview)
PrintPreviewQueue QUEUE
PrintPreviewImage   STRING(80)
                  END
#ENDIF
  CODE
  #EMBED(%ProcedureSetup,'Procedure Setup')
  LocalRequest = GlobalRequest
  LocalResponse = RequestCancelled
  CLEAR(GlobalRequest)
  CLEAR(GlobalResponse)
  #INSERT(%StandardFormula,'Procedure Setup')
  #INSERT(%FileControlOpen)
  #EMBED(%BeforeAccept,'Preparing to Process the Window')
  #MESSAGE('Accept Handling',3)
  #IF(%PrimaryKey)
    #IF(%RecordFilter OR %RangeField)
  RecordsToProcess = %ApproxRecordCount
    #ELSE
  RecordsToProcess = RECORDS(%Primary)
    #ENDIF
  RecordsPerCycle = 25
  #ELSE
    #IF(%RecordFilter)
  RecordsToProcess = %ApproxRecordCount
    #ELSE
  RecordsToProcess = BYTES(%Primary)
    #ENDIF
  RecordsPerCycle = 1000
  #ENDIF
  RecordsProcessed = 0
  PercentProgress = 0
  #EMBED(%BeforeOpeningWindow,'Before Opening Progress Window')
  OPEN(ProgressWindow)
  ?Progress:Thermometer{Prop:Width} = 0
  ?Progress:PctText{Prop:Text} = '0.0% Completed'
  #IF(%EnablePrintPreview)
    ProgressWindow{Prop:Text} = 'Generating Report'
  #ELSE
    ProgressWindow{Prop:Text} = 'Printing Report'
  #ENDIF
  #IF(SUB(%WindowMessage,1,1)='!')
    #SET(%ValueConstruct,SUB(%WindowMessage,2,LEN(%WindowMessage)-1))
  ?Progress:UserString{Prop:Text}=%ValueConstruct
  #ELSE
  ?Progress:UserString{Prop:Text}='%'WindowMessage'
  #ENDIF
  #EMBED(%AfterOpeningWindow,'After Opening Progress Window')
  #IF(%EnableQuickScan)
  #EMBED(%BeforeTurnQuickScanOn,'Before Turning QuickScan On'),WHERE(%EnableQuickScan)
  IF SEND(%Primary,'QUICKSCAN=on').
    #FOR(%Secondary),WHERE(%SecondaryType = '1:MANY')
  IF SEND(%Secondary,'QUICKSCAN=on').
    #ENDFOR
  #EMBED(%AfterTurnQuickScanOn,'After Turning QuickScan On'),WHERE(%EnableQuickScan)
  #ENDIF
  ACCEPT
    CASE EVENT()
    OF Event:OpenWindow
      #INSERT(%ProcessSaveLimits)
      #INSERT(%ReportEventOpenWindow)
    OF Event:Timer
      LOOP RecordsPerCycle TIMES
        #INSERT(%ReportEventTimer)
        LOOP
          #EMBED(%BeforeSubsequentRead,'Before subsequent record retrieval')
          DO GetNextRecord
          #EMBED(%AfterSubsequentRead,'After subsequent record retrieval')
          DO ValidateRecord
          EXECUTE RecordStatus
            BEGIN
              LocalResponse = RequestCancelled
              BREAK
            END
            CYCLE
          END
          BREAK
        END
        IF LocalResponse = RequestCancelled
          LocalResponse = RequestCompleted
          BREAK
        END
        LocalResponse = RequestCancelled
      END
      IF LocalResponse = RequestCompleted
        POST(Event:CloseWindow)
      END
    END
    CASE FIELD()
    OF ?Progress:Cancel
      CASE Event()
      OF Event:Accepted
        LocalResponse = RequestCancelled
        POST(Event:CloseWindow)
      END
    END
  END
  #INSERT(%ReportEndOfProcedure)
  DO ProcedureReturn
#!---------------------------------------------------------------------
ProcedureReturn ROUTINE
  #INSERT(%FileControlClose)
  #EMBED(%EndOfProcedure,'End of Procedure')
  #INSERT(%StandardFormula,'Procedure Exit')
  IF LocalResponse
    GlobalResponse = LocalResponse
  ELSE
    GlobalResponse = RequestCancelled
  END
  #IF(%ReturnValue)
  RETURN(%ReturnValue)
  #ELSE
  RETURN
  #ENDIF
#INSERT(%ProcessValidateRecord)
#INSERT(%ProcessGetRecords)
#!---------------------------------------------------------------------
#EMBED(%ProcRoutines,'Procedure Routines')
#INSERT(%FileControlUpdate)
#DEFAULT
NAME DefaultReport
[COMMON]
DESCRIPTION 'A reporting procedure'
FROM Clarion Report
[DATA]
RejectRecord             LONG,AUTO
LocalRequest             LONG,AUTO
LocalResponse            LONG,AUTO
WindowOpened             LONG,AUTO
RecordsToProcess         LONG,AUTO
RecordsProcessed         LONG,AUTO
RecordsPerCycle          LONG,AUTO
RecordsThisCycle         LONG,AUTO
PercentProgress          DECIMAL(4,1)
RecordStatus             BYTE,AUTO
[PROMPTS]
%RangeLimitType STRING  ('Current Value')
[REPORT]
Report REPORT,AT(1000,2000,6000,7000),PRE(RPT),THOUS
       HEADER,AT(1000,1000,6000,1000)
       END
detail DETAIL
       END
       FOOTER,AT(1000,9000,6000,1000)
       END
       FORM,AT(1000,1000,6000,9000)
       END
     END

#ENDDEFAULT
#INCLUDE('GPReport.TPW')
