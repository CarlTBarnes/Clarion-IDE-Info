#GROUP(%GenerateStandardFunctions)
#INSERT(%GenerateRICode)
#CREATE(%BuildFile)
#MESSAGE('Generating Module:    ' & %SFFileName, 1) #! Post generation message
#MESSAGE('Generating Procedure:  CheckOpen', 2) #! Post generation message
#INSERT(%GenerateScrollStops)
#MESSAGE('Generating CheckOpen',3)
CheckOpen         PROCEDURE(File,OverrideCreate,OverrideOpenMode)

  CODE
  #EMBED(%CheckOpenSetup,'CheckOpen: Setup Procedure')
  IF OMITTED(3)
  #CASE(%DefaultOpenMode)
  #OF('Share')
    #SET(%AccessMode,'42h')
  #OF('Open')
    #SET(%AccessMode,'22h')
  #OF('Other')
    #CASE(%DefaultOtherAccess)
    #OF('Deny None')
      #SET(%AccessMode,'+DenyNone')
    #OF('Deny Read')
      #SET(%AccessMode,'+DenyRead')
    #OF('Deny Write')
      #SET(%AccessMode,'+DenyWrite')
    #OF('Deny All')
      #SET(%AccessMode,'+DenyAll')
    #OF('Any Access')
      #SET(%AccessMode,'+AnyAccess')
    #ENDCASE
    #CASE(%OverrideUserAccess)
    #OF('Read/Write')
      #SET(%AccessMode,',ReadWrite' & %AccessMode)
    #OF('Read Only')
      #SET(%AccessMode,',ReadOnly' & %AccessMode)
    #OF('Write Only')
      #SET(%AccessMode,',WriteOnly' & %AccessMode)
    #ENDCASE
  #ENDCASE
    OPEN(File,%AccessMode)                     #<!Attempt to open the file
  ELSE
    OPEN(File,OverrideOpenMode)
  END
  CASE ERRORCODE()                             #<! and check for errors
  OF NoError                                   #<!Return if no error
    #EMBED(%CheckOpenNoError,'CheckOpen: No Error')
  OROF IsOpenErr                               #<! or if already open.
    #EMBED(%CheckOpenFileOpen,'CheckOpen: File Open Error')
    DO ProcedureReturn
  OF NoFileErr                                 #<!If file was not found
    #EMBED(%CheckOpenNoFile,'CheckOpen: No File Error')
    IF OMITTED(2)
    #IF(%DefaultCreate = 'Create All')
      DO CreateFile
    #ELSIF(%DefaultCreate = 'Create None')
      IF StandardWarning(Warn:CreateError,NAME(File)).
    #ENDIF
    ELSIF OverrideCreate = TRUE
      DO CreateFile
    ELSE
      IF StandardWarning(Warn:CreateError,NAME(File)).
    END
  OF InvalidFileErr                            #<!Invalid Record Declaration
    #EMBED(%CheckOpenBadFile,'CheckOpen: Invalid File Error')
    IF StandardWarning(Warn:InvalidFile,NAME(File)).
  OF BadKeyErr                                 #<!Key Files must be rebuilt
    #EMBED(%CheckOpenBadKey,'CheckOpen: Bad Key Error')
    IF StandardWarning(Warn:InvalidKey,NAME(File))
      BUILD(File)                              !Rebuild the key files
    END
    IF ERRORCODE()
      IF StandardWarning(Warn:RebuildError,NAME(File)).
    ELSE
      IF OMITTED(3)
        OPEN(File,%AccessMode)                 #<!Attempt to open the file
      ELSE
        OPEN(File,OverrideOpenMode)
      END
    END
  END                                          #<!End of Case Structure
  IF ERRORCODE()
    #EMBED(%CheckOpenFileError,'Check Open: Driver Error Handling')
    IF StandardWarning(Warn:DiskError,NAME(File)) THEN HALT(0,'Disk Error').
  END
  DO ProcedureReturn
ProcedureReturn ROUTINE
  #EMBED(%CheckOpenExit,'CheckOpen: Directly before return')
  RETURN
CreateFile ROUTINE
  CREATE(File)                               #<!Create the file
  IF ERRORCODE()
    IF ERRORCODE() = 90
      IF StandardWarning(Warn:CreateError,NAME(File)).
    ELSE
      IF StandardWarning(Warn:CreateError,NAME(File)).
    END
  END
  IF OMITTED(3)
    OPEN(File,%AccessMode)                 #<!Attempt to open the file
  ELSE
    OPEN(File,OverrideOpenMode)
  END
  IF ~ERRORCODE()                            #<!  And return if it opened
    DO ProcedureReturn
  ELSE
    IF StandardWarning(Warn:CreateOpenError,NAME(File)).
  END

#MESSAGE('Generating Procedure:  ReportPreview', 2) #! Post generation message
ReportPreview        PROCEDURE(PrintPreviewQueue)
LeftPageNumber       SHORT,AUTO
RightPageNumber      SHORT,AUTO
Preview:OpMode       BYTE,AUTO
Preview:Op:Normal    EQUATE(0)
Preview:Op:Zoom      EQUATE(1)
Preview:Op:Facing    EQUATE(2)
Preview:Zoomed       BYTE,AUTO
Preview:Facing       BYTE,AUTO
Preview:Pages        BYTE,AUTO
Preview:Jump         LONG,AUTO
PreviewWindow WINDOW('Print Preview'),AT(,,300,180),CENTER,STATUS(200,25,50,-1),SYSTEM,GRAY
       MENUBAR,NOMERGE
         ITEM('&Cancel!'),USE(?PreviewCancel),MSG('Cancel the printing of this report')
         ITEM('&Print!'),USE(?PreviewPrint),MSG('Print this report')
         ITEM('&Zoom In!'),USE(?PreviewZoom),MSG('Zoom in on a page of this report')
         ITEM('&Previous Page!'),USE(?PreviewPreviousPage),MSG('View the previous page(s) of this report'), |
             KEY(PgUpKey)
         ITEM('&Next Page!'),USE(?PreviewNextPage),MSG('View the next page(s) of this report'),KEY(PgDnKey)
         ITEM('&Jump!'),USE(?PreviewJump),MSG('Jump to a selected page of the report')
         ITEM('&Facing Pages!'),USE(?PreviewFacing),MSG('View the report two pages at a time')
       END
       BOX,AT(9,2,136,176),USE(?Page1Frame)
       IMAGE,AT(9,2,136,176),USE(?Page1Image)
       REGION,AT(9,2,136,176),USE(?Page1Zoom)
       BOX,AT(155,2,136,176),USE(?Page2Frame)
       IMAGE,AT(155,2,136,176),USE(?Page2Image)
       REGION,AT(155,2,136,176),USE(?Page2Zoom)
       BOX,AT(84,2,136,176),USE(?SingleFrame)
       IMAGE,AT(84,2,136,176),USE(?SingleImage)
       REGION,AT(84,2,136,176),USE(?SingleZoom)
       IMAGE,AT(0,0),FULL,USE(?SingleZoomImage),HVSCROLL
     END
JumpWindow WINDOW('Jump to a Report Page'),AT(,,150,18),FONT('MS Sans Serif',8,,),CENTER,STATUS,GRAY
       PROMPT('Page:'),AT(4,4,,),USE(?JumpPrompt)
       SPIN(@n5),AT(27,4,32,10),USE(Preview:Jump),RANGE(1,100),STEP(1),MSG('Select a page of the report')
       BUTTON('OK'),AT(74,4,32,10),USE(?OKButton),DEFAULT,MSG('Jump to the selected page')
       BUTTON('Cancel'),AT(111,4,32,10),USE(?CancelButton),MSG('Cancel this selection')
     END
   CODE
   GlobalResponse = RequestCancelled
   OPEN(PreviewWindow)
   RightPageNumber = 1
   Preview:OpMode = Preview:Op:Normal
   Preview:Zoomed = False
   Preview:Facing = False
   IF RECORDS(PrintPreviewQueue) = 1
     DISABLE(?PreviewFacing)
   END
   DO LoadPages
   ACCEPT
     CASE EVENT()
     OF Event:Accepted
       CASE FIELD()
       OF ?SingleZoom
       OROF ?Page2Zoom
         POST(Event:Accepted,?PreviewZoom)
       OF ?Page1Zoom
         RightPagenumber = LeftPageNumber
         POST(Event:Accepted,?PreviewZoom)
       OF ?PreviewCancel
         POST(Event:CloseWindow)
       OF ?PreviewPrint
         GlobalResponse = RequestCompleted
         POST(Event:CloseWindow)
       OF ?PreviewPreviousPage
         IF Preview:OpMode = Preview:Op:Facing
           RightPageNumber -= 2
         ELSE
           RightPageNumber -= 1
         END
         DO LoadPages
       OF ?PreviewNextPage
         IF Preview:OpMode = Preview:Op:Facing
           RightPageNumber += 2
         ELSE
           RightPageNumber += 1
         END
         DO LoadPages
       OF ?PreviewZoom
         IF Preview:OpMode = Preview:Op:Zoom
           ?PreviewFacing{Prop:Disable} = False
           Preview:Zoomed = False
           ?PreviewZoom{Prop:Text} = '&Zoom In!'
           ?PreviewZoom{Prop:Msg} = 'Zoom in on a page of this report'
           IF Preview:Facing
             Preview:OpMode = Preview:Op:Facing
           ELSE
             Preview:OpMode = Preview:Op:Normal
           END
         ELSE
           ?PreviewFacing{Prop:Disable} = True
           Preview:OpMode = Preview:Op:Zoom
           Preview:Zoomed = False
           ?PreviewZoom{Prop:Text} = '&Zoom Out!'
           IF Preview:Facing
             ?PreviewZoom{Prop:Msg} = 'Zoom out to facing page mode'
           ELSE
             ?PreviewZoom{Prop:Msg} = 'Zoom out to full page mode'
           END
         END
         DO LoadPages
       OF ?PreviewFacing
         IF Preview:OpMode = Preview:Op:Facing
           IF RightPageNumber > RECORDS(PrintPreviewQueue)
             RightPageNumber = LeftPageNumber
           END
           Preview:OpMode = Preview:Op:Normal
           Preview:Facing = False
           ?PreviewFacing{Prop:Text} = '&Facing Pages!'
           ?PreviewFacing{Prop:Msg} = 'View the report two pages at a time'
         ELSE
           IF RightPageNumber % 2 = 0
             RightPageNumber += 1
             LeftPageNumber += 1
           END
           Preview:Facing = True
           Preview:OpMode = Preview:Op:Facing
           ?PreviewFacing{Prop:Text} = '&Single Pages!'
           ?PreviewFacing{Prop:Msg} = 'View the report one page at a time'
         END
         DO LoadPages
       OF ?PreviewJump
         OPEN(JumpWindow)
         IF RightPageNumber > RECORDS(PrintPreviewQueue)
           Preview:Jump = LeftPageNumber
         ELSE
           Preview:Jump = RightPageNumber
         END
         ?Preview:Jump{Prop:RangeHigh} = RECORDS(PrintPreviewQueue)
         ACCEPT
           CASE ACCEPTED()
           OF ?OKButton
             RightPageNumber = Preview:Jump
             POST(Event:CloseWindow)
           OF ?CancelButton
             POST(Event:CloseWindow)
           END
         END
         CLOSE(JumpWindow)
         DO LoadPages
       END
     END
   END
   RETURN

LoadPages ROUTINE
   LeftPageNumber = RightPageNumber - 1
   IF Preview:OpMode = Preview:Op:Normal
     IF ?Page1Image{Prop:Hide} = False
       HIDE(?Page1Image)
       HIDE(?Page1Frame)
       DISABLE(?Page1Zoom)
     END
     IF ?Page2Image{Prop:Hide} = False
       HIDE(?Page2Frame)
       HIDE(?Page2Image)
       DISABLE(?Page2Zoom)
     END
     IF ?SingleZoomImage{Prop:Hide} = False
       HIDE(?SingleZoomImage)
     END
     IF ?SingleImage{Prop:Hide} = True
       UNHIDE(?SingleImage)
       UNHIDE(?SingleFrame)
       ENABLE(?SingleZoom)
     END
     GET(PrintPreviewQueue,RightPageNumber)
     ?SingleImage{Prop:Text} = PrintPreviewQueue
     PreviewWindow{Prop:StatusText,2} = ''
     PreviewWindow{Prop:StatusText,3} = 'Single Page'
     PreviewWindow{Prop:StatusText,4} = 'Page ' & RightPageNumber & ' of ' & RECORDS(PrintPreviewQueue)
   ELSIF Preview:OpMode = Preview:Op:Facing
     IF RightPageNumber % 2 = 0
       RightPageNumber += 1
       LeftPageNumber += 1
     END
     IF ?SingleZoomImage{Prop:Hide} = False
       HIDE(?SingleZoomImage)
     END
     IF ?SingleImage{Prop:Hide} = False
       HIDE(?SingleImage)
       HIDE(?SingleFrame)
       DISABLE(?SingleZoom)
     END
     IF LeftPageNumber = 0
       IF ?Page1Image{Prop:Hide} = False
         HIDE(?Page1Image)
         HIDE(?Page1Frame)
         DISABLE(?Page1Zoom)
       END
     ELSE
       IF ?Page1Image{Prop:Hide} = True
         UNHIDE(?Page1Image)
         UNHIDE(?Page1Frame)
         ENABLE(?Page1Zoom)
       END
       GET(PrintPreviewQueue,LeftPageNumber)
       ?Page1Image{Prop:Text} = PrintPreviewQueue
     END
     IF LeftPageNumber = RECORDS(PrintPreviewQueue)
       IF ?Page2Image{Prop:Hide} = False
         HIDE(?Page2Image)
         HIDE(?Page2Frame)
         DISABLE(?Page2Zoom)
       END
     ELSE
       IF ?Page2Image{Prop:Hide} = True
         UNHIDE(?Page2Image)
         UNHIDE(?Page2Frame)
         ENABLE(?Page2Zoom)
       END
       GET(PrintPreviewQueue,RightPageNumber)
       ?Page2Image{Prop:Text} = PrintPreviewQueue
     END
     IF LeftPageNumber AND RightPageNumber =< RECORDS(PrintPreviewQueue)
       PreviewWindow{Prop:StatusText,4} = 'Pages ' & LeftPageNumber & ' and ' & RightPageNumber & ' of ' & RECORDS(PrintPreviewQueue)
     ELSIF LeftPageNumber AND RightPageNumber > RECORDS(PrintPreviewQueue)
       PreviewWindow{Prop:StatusText,4} = 'Page ' & LeftPageNumber & ' of ' & RECORDS(PrintPreviewQueue)
     ELSE
       PreviewWindow{Prop:StatusText,4} = 'Page ' & RightPageNumber & ' of ' & RECORDS(PrintPreviewQueue)
     END
     PreviewWindow{Prop:StatusText,2} = ''
     PreviewWindow{Prop:StatusText,3} = 'Facing Pages'
   ELSE
     IF ?Page1Image{Prop:Hide} = False
       HIDE(?Page1Image)
       HIDE(?Page1Frame)
       DISABLE(?Page1Zoom)
     END
     IF ?Page2Image{Prop:Hide} = False
       HIDE(?Page2Frame)
       HIDE(?Page2Image)
       DISABLE(?Page2Zoom)
     END
     IF ?SingleZoomImage{Prop:Hide} = False
       HIDE(?SingleZoomImage)
     END
     IF ?SingleImage{Prop:Hide} = False
       HIDE(?SingleImage)
       HIDE(?SingleFrame)
       DISABLE(?SingleZoom)
     END
     GET(PrintPreviewQueue,RightPageNumber)
     ?SingleZoomImage{Prop:Text} = PrintPreviewQueue
     UNHIDE(?SingleZoomImage)
     PreviewWindow{Prop:StatusText,2} = 'ZOOM'
     PreviewWindow{Prop:StatusText,3} = 'Single Page'
     PreviewWindow{Prop:StatusText,4} = 'Page ' & RightPageNumber & ' of ' & RECORDS(PrintPreviewQueue)
   END
   IF RightPageNumber = 1
     DISABLE(?PreviewPreviousPage)
   ELSE
     ENABLE(?PreviewPreviousPage)
   END
   IF RightPageNumber => RECORDS(PrintPreviewQueue)
     DISABLE(?PreviewNextPage)
   ELSE
     ENABLE(?PreviewNextPage)
   END

#MESSAGE('Generating Procedure:  INIRestoreWindow', 2) #! Post generation message
INIRestoreWindow   PROCEDURE(ProcedureName,INIFileName)

INI::XPosit        LONG,AUTO
INI::YPosit        LONG,AUTO
INI::Width         LONG,AUTO
INI::Height        LONG,AUTO
INI::String        STRING(20),AUTO

  CODE
  INI::XPosit = GETINI(ProcedureName,'XPosit',,INIFileName)
  IF NOT INI::XPosit THEN RETURN.
  LOOP
    IF TARGET{Prop:Maximize} = FALSE
      INI::XPosit = GETINI(ProcedureName,'XPosit',,INIFileName)
      IF NOT INI::XPosit THEN BREAK.
      INI::YPosit = GETINI(ProcedureName,'YPosit',,INIFileName)
      IF NOT INI::YPosit THEN BREAK.
      IF TARGET{Prop:Resize}
        INI::Width = GETINI(ProcedureName,'Width',,INIFileName)
        IF NOT INI::Width THEN BREAK.
        INI::Height = GETINI(ProcedureName,'Height',,INIFileName)
        IF NOT INI::Height THEN BREAK.
      ELSE
        INI::Width = TARGET{Prop:Width}
        INI::Height = TARGET{Prop:Height}
      END
      SETPOSITION(0,INI::XPosit,INI::YPosit,INI::Width,INI::Height)
    END
    BREAK
  END
  INI::String = GETINI(ProcedureName,'Maximize',,INIFileName)
  IF INI::String = 'Yes'
    TARGET{Prop:Maximize}=1
  ELSE
    TARGET{Prop:Maximize}=0
  END
  RETURN

#MESSAGE('Generating Procedure:  INISaveWindow', 2) #! Post generation message
INISaveWindow   PROCEDURE(ProcedureName,INIFileName)
INI::Section       CSTRING(80)
INI::String        CSTRING(20)
INI::Entry         CSTRING(20)
  CODE
  INI::Section = ProcedureName
  INI::Entry = 'Maximize'
  IF Target{Prop:Maximize}
    INI::String = 'Yes'
  ELSE
    INI::String = 'No'
  END
  TARGET{PROP:Maximize} = 0
  IF Target{Prop:Iconize}
    RETURN
  END
  PUTINI(INI::Section,INI::Entry,INI::String,INIFileName)
  INI::Entry = 'XPosit'
  INI::String = TARGET{Prop:XPos}
  PUTINI(INI::Section,INI::Entry,INI::String,INIFileName)
  INI::Entry = 'YPosit'
  INI::String = TARGET{Prop:YPos}
  PUTINI(INI::Section,INI::Entry,INI::String,INIFileName)
  INI::Entry = 'Width'
  INI::String = TARGET{Prop:Width}
  PUTINI(INI::Section,INI::Entry,INI::String,INIFileName)
  INI::Entry = 'Height'
  INI::String = TARGET{Prop:Height}
  PUTINI(INI::Section,INI::Entry,INI::String,INIFileName)
  RETURN

#MESSAGE('Generating Procedure:  StandardWarning', 2) #! Post generation message
StandardWarning      FUNCTION(WarningID,WarningText1,WarningText2,WarningText3,WarningText4)
ErrorText            STRING(150),AUTO
ReturnValue          LONG
  CODE
  IF ERRORCODE() <> 90
    ErrorText = CLIP(ERROR()) & ' (' & ERRORCODE() & ')'
  ELSE
    ErrorText = CLIP(FILEERROR()) & ' (' & CLIP(FILEERRORCODE()) & ')'
  END
  CASE WarningID
  OF Warn:InvalidFile
    MESSAGE('Error: (' & CLIP(ErrorText) & ') accessing ' |
    & CLIP(WarningText1) & '.  Press OK to end this application.'|
    ,'Invalid File',ICON:Exclamation,Button:OK,BUTTON:OK,0)
    HALT(0,'Invalid File!')
  OF Warn:InvalidKey     
    IF MESSAGE(CLIP(WarningText1) & ' key file is invalid.  Do you '|
    &'want to rebuild the key?','Invalid Key',Icon:Question,|
    Button:Yes+Button:No,Button:Yes,0)=Button:No
      HALT(0,'Invalid Key!')
    ELSE
      RETURN(Button:Yes)
    END
  OF Warn:RebuildError   
    MESSAGE('Error: (' & CLIP(ErrorText) & ') repairing key for ' |
    & CLIP(WarningText1) & '.  Press OK to end this application.',|
    'Key Rebuild Error',ICON:Exclamation,Button:OK,BUTTON:OK,0)
    HALT(0,'Error Rebuilding Key!')
  OF Warn:CreateError    
    MESSAGE('Error: (' & CLIP(ErrorText) & ') creating ' |
    & CLIP(WarningText1) & '.  Press OK to end this application.',|
    'File Creation Error',ICON:Exclamation,Button:OK,BUTTON:OK,0)
    HALT(0,'File Creation Error!')
  OF Warn:CreateOpenError
    MESSAGE('Error: (' & CLIP(ErrorText) & ') opening created ' |
    & 'file:' & CLIP(WarningText1) & '.  Press OK to end this application.',|
    'File Creation Error',ICON:Exclamation,Button:OK,BUTTON:OK,0)
    HALT(0,'File Creation Error!')
  OF Warn:ProcedureToDo  
    RETURN(MESSAGE('The Procedure ' & CLIP(WarningText1) & ' has not '|
    &'been defined.','Procedure not defined',ICON:Exclamation,|
    Button:OK,BUTTON:OK,0))
  OF Warn:BadKeyedRec
    RETURN(MESSAGE('Unable to read keyed record.  Error: ' |
    & CLIP(ErrorText) & '.  Insert Aborted',ICON:Exclamation,|
    Button:OK,Button:OK,0))
  OF Warn:OutOfRangeHigh
    RETURN(MESSAGE('The value of ' & CLIP(WarningText1) & ' must'|
    &' be lower than ' & CLIP(WarningText2) & '.','Range Error',|
    ICON:Exclamation,Button:OK,Button:OK,0))
  OF Warn:OutOfRangeLow  
    RETURN(MESSAGE('The value of ' & CLIP(WarningText1) & ' must be'|
    &' higher than ' & CLIP(WarningText2) & '.','Range Error',|
    ICON:Exclamation,Button:OK,Button:OK,0))
  OF Warn:OutOfRange     
    RETURN(MESSAGE('The value of ' & CLIP(WarningText1) & ' must be '|
    &'between ' & CLIP(WarningText2) & ' and ' & CLIP(WarningText3) |
    & '.','Range Error',ICON:Exclamation,Button:OK,Button:OK,0))
  OF Warn:NotInFile
    RETURN(MESSAGE('The value for ' & CLIP(WarningText1) & ' must be '|
    &'found in the ' & CLIP(WarningText2) & ' file.','Field Contents '|
    &'Error',ICON:Exclamation,Button:OK,Button:OK,0))
  OF Warn:RestrictUpdate 
    RETURN(MESSAGE('This record is referenced from the file '|
    & CLIP(WarningText1) & '.  Linking field(s) have been restricted'|
    & ' from change and have been reset to original values.',|
    'Referential Integrity Update Error',ICON:Exclamation,|
    Button:OK,Button:OK,0))
  OF Warn:RestrictDelete 
    RETURN(MESSAGE('This record is referenced from the file '|
    & CLIP(WarningText1) & '.  This record cannot be deleted while'|
    & ' these references exist.','Referential Integrity Delete Error'|
    ,ICON:Exclamation,Button:OK,Button:OK,0))
  OF Warn:InsertError
    RETURN(MESSAGE('An error was experienced during the update of'|
    & ' record.  Error: ' & CLIP(ErrorText) & '.'|
    ,'Record Insert Error'|
    ,ICON:Exclamation,Button:OK,Button:OK,0))
  OF Warn:RIUpdateError
    IF ERRORCODE() AND ERRORCODE() <> RecordChangedErr
      RETURN(MESSAGE('An error (' & CLIP(ErrorText) & ') was experienced'|
      &' when attempting to update a record from the file.  Probable Cause: ' |
      & CLIP(WarningText1) & '.','Update Operation Error',Icon:Exclamation,|
      Button:OK,Button:OK,0))
    ELSE
      RETURN(MESSAGE('This record was changed by another station.'|
      ,'Update Operation Error',Icon:Exclamation,|
      Button:OK,Button:OK,0))
    END
  OF Warn:UpdateError
    RETURN(MESSAGE('An error was experienced changing this record.  '|
    &'Do you want to try to save again?','Record Update Error',|
    Icon:Exclamation,Button:Yes+Button:No+Button:Cancel,Button:Cancel,0))
  OF Warn:RIDeleteError
    RETURN(MESSAGE('An error (' & CLIP(ErrorText) & ') was experienced'|
    &' when attempting to delete a record from the file ' |
    & CLIP(WarningText1) & '.','Delete Operation Error',Icon:Exclamation,|
    Button:OK,Button:OK,0))
  OF Warn:DeleteError    
    RETURN(MESSAGE('An error was experienced deleting this record.  '|
    &'Do you want to try to save again?','Record Update Error',|
    Icon:Exclamation,Button:Yes+Button:No+Button:Cancel,Button:Cancel,0))
  OF Warn:InsertDisabled 
    RETURN(MESSAGE('This procedure was called to insert a record, '|
    & 'however inserts are not allowed for this procedure.  Press OK '|
    & 'to return to the calling procedure','Invalid Request',|
    Icon:Exclamation,Button:OK,Button:OK,0))
  OF Warn:UpdateDisabled
    RETURN(MESSAGE('This procedure was called to change a record, '|
    & 'however changes are not allowed for this procedure.  Press OK '|
    & 'to return to the calling procedure','Invalid Request',|
    ICON:Exclamation,Button:OK,Button:OK,0))
  OF Warn:DeleteDisabled 
    RETURN(MESSAGE('This procedure was called to delete a record, '|
    & 'however deletions are not allowed for this procedure.  Press OK '|
    & 'to return to the calling procedure','Invalid Request',|
    ICON:Exclamation,Button:OK,Button:OK,0))
  OF Warn:NoCreate       
    MESSAGE('The File ' & CLIP(WarningText1) & 'was not found, '|
    &'and creation of the file is not allowed.  Press OK to end '|
    &'this application.','File Creation Not Allowed',ICON:Exclamation,|
    Button:OK,BUTTON:OK,0)
    HALT(0,'File Creation Error!')
  OF Warn:ConfirmCancel  
    RETURN(MESSAGE('Are you sure you want to cancel?'|
    ,'Update Cancelled',ICON:Question,Button:Yes+Button:No,|
    Button:No,0))
  OF Warn:DuplicateKey
    RETURN(MESSAGE('Adding this record creates a duplicate entry '|
    &'for the key:' & CLIP(WarningText1),'Duplicate Key Error',|
    ICON:Exclamation,Button:OK,Button:OK,0))
  OF Warn:AutoIncError
    RETURN(MESSAGE('Attempts to automatically number this record have '|
    &'failed.  Error: ' & CLIP(ErrorText) & '.',|
    'Auto Increment Error',Icon:Exclamation,Button:Cancel+Button:Retry,|
    Button:Cancel,0))
  OF Warn:FileLoadError
    RETURN(MESSAGE(CLIP(WarningText1) & ' File Load Error.  '|
    &'Error: ' & CLIP(ErrorText) & '.','File Load Error',ICON:Exclamation,|
    Button:OK,Button:OK,0))
  OF Warn:ConfirmCancelLoad
    RETURN(MESSAGE('Are you certain you want to stop loading ' |
    & CLIP(WarningText1) & '?','Cancel Request',|
    ICON:Question,Button:OK+Button:Cancel,Button:Cancel,0))
  OF Warn:FileZeroLength
    RETURN(MESSAGE(CLIP(WarningText1) & ' File Load Error.  '|
    &'The file you''ve requested contains no text.','File Load Error',|
    ICON:Exclamation,Button:OK,Button:OK,0))
  OF Warn:EndOfASCIIQueue
    IF WarningText1 = 'Down'
      RETURN(MESSAGE('The end of the viewed file was encountered.  '|
      & 'Do you want to start again from the beginning?',|
      'End of File Error',ICON:Question,Button:Yes+Button:No,Button:Yes,0))
    ELSE
      RETURN(MESSAGE('The beginning of the viewed file was encountered.  '|
      & 'Do you want to start again from the end of the file?',|
      'Beginning of File Error',ICON:Question,Button:Yes+Button:No,|
      Button:Yes,0))
    END
  OF Warn:DiskError
    RETURN(MESSAGE('File (' & CLIP(WarningText1) & ') could not be '|
    & 'opened.  Error: ' & CLIP(ErrorText) & '.','File Access Error'|
    ,Icon:Exclamation,Button:OK,Button:OK,0))
  OF Warn:ProcessActionError
    IF WarningText1 = 'Put'
      RETURN(MESSAGE('An error was experienced when making changes'|
      & ' to the ' & CLIP(WarningText2) & ' file.  Error: '|
      & CLIP(ErrorText),'Process PUT Error',Icon:Exclamation|
      ,Button:OK,Button:OK,0))
    ELSE
      RETURN(MESSAGE('An error was experienced when deleting a record'|
      & ' from the ' & CLIP(WarningText2) & ' file.  Error: '|
      & CLIP(ErrorText),'Process DELETE Error',Icon:Exclamation|
      ,Button:OK,Button:OK,0))
    END
  OF Warn:StandardDelete
    RETURN(MESSAGE('You''ve selected to delete the highlighted record.  '|
    &'Press OK to confirm deletion of this record.',|
    'Confirm Delete',Icon:Question,Button:OK+Button:Cancel,Button:Cancel,0))
  OF Warn:SaveOnCancel
    RETURN(MESSAGE('Do you want to save the changes to this record?'|
    ,'Update Cancelled',ICON:Question,Button:Yes+Button:No+Button:Cancel,|
    Button:No,0))
  OF Warn:LogoutError
    MESSAGE('Error: (' & CLIP(ErrorText) & ') attempting to frame the ' |
    & LOWER(CLIP(WarningText1)) & ' transaction on ' & CLIP(WarningText2)|
    & '.  ' & CLIP(WarningText1) & ' transaction cancelled.'            |
    ,'Transaction Framing Error',ICON:Exclamation,Button:OK,BUTTON:OK,0)
  OF Warn:RecordFetchError
    MESSAGE('Error: (' & CLIP(ErrorText) & ') attempting to access a ' |
    & 'record from the ' & CLIP(WarningText1) & ' file.  Returning to ' |
    & 'previous window.' |
    ,'Record Retrieval Error',ICON:Exclamation,Button:OK,Button:OK,0)
  OF Warn:ViewOpenError
    MESSAGE('Error: (' & CLIP(ErrorText) & ') attempting to open the ' |
    & 'current VIEW. Filter and Range Limits ignored.' |
    ,'View Open Error',ICON:Exclamation,Button:OK,BUTTON:OK,0)
  END

#CLOSE(%BuildFile)
#REPLACE(%SFFileName,%BuildFile)
#GROUP(%GenerateScrollStops)
#MESSAGE('Generating FirstStringStop',3)
                  MEMBER('%Program')
                   GROUP,PRE(CS:)
OverrideCharacters   STRING('`!"£$%%^&*()''-=_+][#;~@:/.,?\| ')
NumericCharacters    STRING('0123456789')
SortCharacters       STRING(255)
SortCharacterCount   LONG
StringRoot           STRING(255)
StringRootLen        LONG
StepValue            LONG
CurrentStep          LONG
LowValue             LONG
HighValue            LONG
LowString            STRING(4)
HighString           STRING(4)
                   END

                   GROUP,PRE(CR:)
StepValue            REAL
CurrentStep          LONG
LowLimit             REAL
HighLimit            REAL
                   END

SetupRealStops PROCEDURE(InputLowLimit,InputHighLimit)
  CODE
  CR::LowLimit = InputLowLimit
  CR::HighLimit = InputHighLimit
  CR::StepValue = (InputHighLimit - InputLowLimit) / 99
  CR::CurrentStep = 0
  RETURN

NextRealStop  FUNCTION
  CODE
  CR::CurrentStep += 1
  CASE CR::CurrentStep
  OF 1
    RETURN(CR::LowLimit)
  OF 100
    RETURN(CR::HighLimit)
  ELSE
    RETURN(CR::LowLimit + (CR::StepValue * (CR::CurrentStep-1)))
  END

SetupStringStops PROCEDURE(ProcessLowLimit,ProcessHighLimit,InputStringSize,ListType)
InputLowLimit      STRING(255)
InputHighLimit     STRING(255)
CurrentStep        LONG
CurrentVal         LONG
LowCharacter       STRING(1)
HighCharacter      STRING(1)
NextCharacter      STRING(1)
  CODE
  IF OMITTED(4)
    Listtype = ScrollSort:AllowAlpha
  END
  CS::SortCharacterCount = 0
  CS::SortCharacters = ''
  LOOP CurrentStep = 1 TO 255
    NextCharacter = CHR(CurrentStep)
    IF INSTRING(NextCharacter,CS::NumericCharacters,1,1)
      IF BAND(Listtype,ScrollSort:AllowNumeric)
        DO AddSortCharacter
      END
    ELSIF INSTRING(NextCharacter,CS::OverrideCharacters,1,1)
      IF BAND(Listtype,ScrollSort:AllowAlt)
        DO AddSortCharacter
      END
    ELSIF ISALPHA(NextCharacter)
      IF BAND(Listtype,ScrollSort:AllowAlpha)
        IF ISUPPER(NextCharacter)
          DO AddSortCharacter
        ELSIF BAND(Listtype,ScrollSort:CaseSensitive)
          DO AddSortCharacter
        END
      END
    END
  END
  CurrentStep = 0
  InputLowLimit = SUB(ProcessLowLimit,1,InputStringSize)
  InputHighLimit = SUB(ProcessHighLimit,1,InputStringSize)
  IF LEN(CLIP(InputHighLimit))<InputStringSize
    NextCharacter = SUB(CS::SortCharacters,CS::SortCharacterCount,1)
    InputHighLimit = CLIP(InputHighLimit) & ALL(NextCharacter,(InputStringSize+1)-LEN(InputHighLimit))
  END
  LOOP CurrentStep = 1 TO InputStringSize
    IF CurrentStep > LEN(InputLowLimit) THEN BREAK.
    LowCharacter = SUB(InputLowLimit,CurrentStep,1)
    HighCharacter = SUB(InputHighLimit,CurrentStep,1)
    IF LowCharacter <> HighCharacter THEN BREAK.
  END
  CS::StringRootLen = CurrentStep - 1
  CS::StringRoot = SUB(InputLowLimit,1,CS::StringRootLen)
  CS::LowString = SUB(InputLowLimit,CurrentStep,4)
  CS::HighString = SUB(InputHighLimit,CurrentStep,4)
  IF BAND(ListType,ScrollSort:AllowAlpha) AND NOT BAND(ListType,ScrollSort:CaseSensitive)
    CS::LowString = UPPER(CS::LowString)
    CS::HighString = UPPER(CS::HighString)
  END
  CurrentStep = 0
  CS::LowValue = 0
  CS::HighValue = 0
  LOOP
    CurrentStep += 1
    IF CurrentStep = 5 THEN BREAK.
    CS::LowValue *= CS::SortCharacterCount + 1
    CS::HighValue *= CS::SortCharacterCount + 1
    This# = That#
    IF CurrentStep ~> LEN(CLIP(CS::LowString))
      NextCharacter = SUB(CS::LowString,CurrentStep,1)
      CurrentVal = INSTRING(NextCharacter,CS::SortCharacters,1,1)
      CS::LowValue += CurrentVal
    END
    IF CurrentStep ~> LEN(CLIP(CS::HighString))
      NextCharacter = SUB(CS::HighString,CurrentStep,1)
      CurrentVal = INSTRING(NextCharacter,CS::SortCharacters,1,1)
      CS::HighValue += CurrentVal
    END
  END
  CS::StepValue = (CS::HighValue - CS::LowValue) / 99
  CS::CurrentStep = 0
  RETURN

#MESSAGE('Generating NextStringStop',3)
AddSortCharacter ROUTINE
  IF CS::SortCharacterCount
    CS::SortCharacters = SUB(CS::SortCharacters,1,CS::SortCharacterCount) & NextCharacter
  ELSE
    CS::SortCharacters = NextCharacter
  END
  CS::SortCharacterCount += 1

NextStringStop  FUNCTION
StepString      STRING(4)
CurrentStop     LONG
CurrentStep     LONG
CurrentVal      LONG
  CODE
  StepString = ''
  CS::CurrentStep += 1
  CASE CS::CurrentStep
  OF 1
    StepString = CS::LowString
  OF 100
    StepString = CS::HighString
  ELSE
    CurrentStop = CS::LowValue + (CS::StepValue * (CS::CurrentStep-1))
    LOOP CurrentStep = 1 TO 4
      CurrentVal = CurrentStop % (CS::SortCharacterCount + 1)
      CurrentStop = (CurrentStop - CurrentVal) / (CS::SortCharacterCount + 1)
      StepString = SUB(CS::SortCharacters,Currentval,1) & StepString
    END
  END
  IF CS::StringRootLen
    RETURN(SUB(CS::StringRoot,1,CS::StringRootLen) & StepString)
  ELSE
    RETURN(StepString)
  END

