#PROGRAM(CLARION,'Default Program'),HLP('~TPLProgram')
#INSERT(%RIGather)
   PROGRAM

#EMBED(%BeforeGlobalIncludes,'Before Global INCLUDEs'),DATA
   INCLUDE('Equates.CLW')
   INCLUDE('TplEqu.CLW')
   INCLUDE('Keycodes.CLW')
   INCLUDE('Errors.CLW')
#EMBED(%AfterGlobalIncludes,'After Global INCLUDEs'),DATA
#!
#! Generate MAP
#!
#MESSAGE('Generating RI Lists',3)
#MESSAGE('Generating MAP',3)
   MAP
#FIX(%Module,%Program)
#FOR(%ModuleProcedure)
  #FIX(%Procedure,%ModuleProcedure)
     %Procedure %Prototype
#ENDFOR
#IF(%ModuleInclude)
     INCLUDE('%ModuleInclude')
#ENDIF
#MESSAGE('Declaring Generated Procedures',3)
#FOR(%CustomGlobalMapModule)
     MODULE(%CustomGlobalMapModule)
  #FOR(%CustomGlobalMapProcedure)
       %CustomGlobalMapProcedure%CustomGlobalMapProcedurePrototype
  #ENDFOR
     END
#ENDFOR
#FOR(%Module), WHERE (%Module <> %Program)
     MODULE('%Module')
  #IF(%ModuleExternal)
    #IF(%ModuleInclude)
       INCLUDE('%ModuleInclude')
    #ELSE
      #FOR(%ModuleProcedure)
        #FIX(%Procedure,%ModuleProcedure)
        #IF(%ModuleTemplate = 'ExternalDLL(Clarion)')
       %Procedure%Prototype,DLL(dll_mode)
        #ELSE
       %Procedure%Prototype
        #ENDIF
      #ENDFOR
    #ENDIF
  #ELSE
    #FOR(%ModuleProcedure)
      #FIX(%Procedure,%ModuleProcedure)
       %Procedure%Prototype
    #ENDFOR
  #ENDIF
     END
#ENDFOR
#MESSAGE('Declaring Standard Functions',3)
     MODULE('%SFFileName')
       CheckOpen(FILE,<BYTE>,<BYTE>)
       ReportPreview(QUEUE)
       StandardWarning(LONG,<STRING>,<STRING>,<STRING>,<STRING>),LONG,PROC
       SetupStringStops(STRING,STRING,LONG,<LONG>)
       NextStringStop,STRING
       SetupRealStops(REAL,REAL)
       NextRealStop,REAL
       INIRestoreWindow(STRING,STRING)
       INISaveWindow(STRING,STRING)
     END
#MESSAGE('Declaring RI Procedures',3)
     MODULE('%RIUpdFileName')
       #INSERT(%RIDeclare,'UPDATE')
     END
     MODULE('%RIDelFileName')
       #INSERT(%RIDeclare,'DELETE')
     END
     #EMBED(%GlobalMap,'Inside the Global Map')
   END


#MESSAGE('Generating Global Data',3)
#FOR(%GlobalData)
%GlobalData   %GlobalDataStatement
#ENDFOR
#FOR(%CustomGlobalData),WHERE(%CustomGlobalDataBeforeFiles)
%[20]CustomGlobalData %CustomGlobalDataDeclaration
  #FOR(%CustomGlobalDataComponent)
%[20 + (%CustomGlobalDataComponentIndent * 2)]CustomGlobalDataComponent %CustomGlobalDataComponentDeclaration
  #ENDFOR
#ENDFOR

#IF(%GlobalExternal)
GlobalRequest        LONG(0),EXTERNAL,DLL(dll_mode)
GlobalResponse       LONG(0),EXTERNAL,DLL(dll_mode)
#ELSE
GlobalRequest        LONG(0),THREAD
GlobalResponse       LONG(0),THREAD
#ENDIF
#EMBED(%BeforeFileDeclarations,'Before File Declarations'),DATA
#FOR(%File)
  #IF(%DefaultGenerate)
  #ELSIF(%OverrideGenerate)
  #ELSE
    #FIX(%UsedFile,%File)
    #IF(NOT %UsedFile)
      #CYCLE
    #ENDIF
  #ENDIF
  #SET(%ValueConstruct,SUB(%FileStruct,INSTRING(' ',%FileStruct,1,1),LEN(%FileStruct)))
  #CASE(%OverrideCreate)
  #OF('Create File')
    #SET(%ValueConstruct,%ValueConstruct & ',CREATE')
  #OF('Do Not Create File')
  #OF('Use Default')
    #CASE(%DefaultCreate)
    #OF('Create All')
      #SET(%ValueConstruct,%ValueConstruct & ',CREATE')
    #OF('Create None')
    #OF('Use File Setting')
      #IF(%FileCreate)
        #SET(%ValueConstruct,%ValueConstruct & ',CREATE')
      #ENDIF
    #ENDCASE
  #OF('')
    #CASE(%DefaultCreate)
    #OF('Create All')
        #SET(%ValueConstruct,%ValueConstruct & ',CREATE')
    #OF('Create None')
    #OF('Use File Setting')
      #IF(%FileCreate)
        #SET(%ValueConstruct,%ValueConstruct & ',CREATE')
      #ENDIF
    #ENDCASE
  #OF('Use File Setting')
    #IF(%FileCreate)
      #SET(%ValueConstruct,%ValueConstruct & ',CREATE')
    #ENDIF
  #ENDCASE
  #IF(%ValueConstruct = %FileStruct)
    #IF(NOT EXTRACT(%ValueConstruct,'BINDABLE'))
      #SET(%ValueConstruct,%ValueConstruct & ',BINDABLE')
    #ENDIF
  #ENDIF
  #SET(%FileThreadedFlag,%False)
  #CASE(%OverrideThreaded)
  #OF('Threaded')
    #SET(%ValueConstruct,%ValueConstruct & ',THREAD')
    #SET(%FileThreadedFlag,%True)
  #OF('Not Threaded')
  #OF('Use Default')
    #CASE(%DefaultThreaded)
    #OF('All Threaded')
      #SET(%ValueConstruct,%ValueConstruct & ',THREAD')
      #SET(%FileThreadedFlag,%True)
    #OF('None Threaded')
    #OF('Use File Setting')
      #IF(%FileThreaded)
        #SET(%ValueConstruct,%ValueConstruct & ',THREAD')
        #SET(%FileThreadedFlag,%True)
      #ENDIF
    #ENDCASE
  #OF('')
    #CASE(%DefaultThreaded)
    #OF('All Threaded')
      #SET(%FileThreadedFlag,%True)
      #SET(%ValueConstruct,%ValueConstruct & ',THREAD')
    #OF('None Threaded')
    #OF('Use File Setting')
      #IF(%FileThreaded)
        #SET(%FileThreadedFlag,%True)
        #SET(%ValueConstruct,%ValueConstruct & ',THREAD')
      #ENDIF
    #ENDCASE
  #OF('Use File Setting')
    #IF(%FileThreaded)
      #SET(%FileThreadedFlag,%True)
      #SET(%ValueConstruct,%ValueConstruct & ',THREAD')
    #ENDIF
  #ENDCASE
  #SET(%FileExternalFlag,%False)
  #CASE(%OverrideExternal)
  #OF('External')
    #IF(%OverrideExternalAPP)
      #SET(%FileExternalFlag,%True)
    #ENDIF
    #SET(%FileExternalFlag,%True)
    #IF(%OverrideExternalSource)
      #SET(%ValueConstruct,%ValueConstruct & ',EXTERNAL(%OverrideExternalSource),DLL(dll_mode)')
    #ELSE
      #SET(%ValueConstruct,%ValueConstruct & ',EXTERNAL(''''),DLL(dll_mode)')
    #ENDIF
  #OF('Use Default')
    #IF(%DefaultExternal='All External')
      #IF(%DefaultExternalAPP)
        #SET(%FileExternalFlag,%True)
      #ENDIF
      #IF(%DefaultExternalSource)
        #SET(%ValueConstruct,%ValueConstruct & ',EXTERNAL(%DefaultExternalSource),DLL(dll_mode)')
      #ELSE
        #SET(%ValueConstruct,%ValueConstruct & ',EXTERNAL(''''),DLL(dll_mode)')
      #ENDIF
    #ENDIF
  #OF('Not External')
  #ELSE
    #IF(%DefaultExternal='All External')
      #IF(%DefaultExternalAPP)
        #SET(%FileExternalFlag,%True)
      #ENDIF
      #IF(%DefaultExternalSource)
        #SET(%ValueConstruct,%ValueConstruct & ',EXTERNAL(%DefaultExternalSource),DLL(dll_mode)')
      #ELSE
        #SET(%ValueConstruct,%ValueConstruct & ',EXTERNAL(''''),DLL(dll_mode)')
      #ENDIF
    #ENDIF
  #ENDCASE
  #IF(EXTRACT(%ValueConstruct,'EXTERNAL'))
    #IF(EXTRACT(%ValueConstruct,'NAME'))
      #SET(%ValueConstruct,REPLACE(%ValueConstruct,'NAME',''))
    #ENDIF
  #ENDIF
#EMBED(%BeforeFileDeclaration,'Before File Declaration'),HIDE
#MESSAGE('File Structure: ' & %File,3)
%File %ValueConstruct
  #FOR(%Key)
%KeyStruct
  #ENDFOR
  #FOR(%Field),WHERE(%FieldType = 'MEMO')
%FieldStruct
  #ENDFOR
  #FOR(%Field),WHERE(%FieldType = 'BLOB')
%FieldStruct
  #ENDFOR
%FileStructRec,PRE()
  #FOR(%Field),WHERE((%FieldType <> 'MEMO') AND (%FieldType <> 'BLOB'))
%FieldStruct
  #ENDFOR
%FileStructRecEnd
%FileStructEnd
  #SET(%ValueConstruct,%File & '::Used')
  #IF(%FileExternalFlag)
%[20]ValueConstruct LONG,EXTERNAL,DLL(dll_mode)
  #ELSE
    #IF(%FileThreadedFlag)
%[20]ValueConstruct LONG,THREAD
    #ELSE
%[20]ValueConstruct LONG
    #ENDIF
  #ENDIF

#ENDFOR
#FOR(%CustomGlobalData),WHERE(NOT %CustomGlobalDataBeforeFiles)
%[20]CustomGlobalData %CustomGlobalDataDeclaration
  #FOR(%CustomGlobalDataComponent)
%[20 + (%CustomGlobalDataComponentIndent * 2)]CustomGlobalDataComponent %CustomGlobalDataComponentDeclaration
  #ENDFOR
#ENDFOR
#EMBED(%AfterFileDeclarations,'After File Declarations'),DATA
Sort:Name STRING(ScrollSort:Name)
Sort:Name:Array STRING(3),DIM(100),OVER(Sort:Name)
Sort:Alpha STRING(ScrollSort:Alpha)
Sort:Alpha:Array STRING(2),DIM(100),OVER(Sort:Alpha)
#!
#EMBED(%GlobalData,'Global Data'),DATA
#!
LocateOnPosition    EQUATE(1)
LocateOnValue       EQUATE(2)
FillBackward        EQUATE(1)
FillForward         EQUATE(2)
RefreshOnPosition   EQUATE(1)
RefreshOnQueue      EQUATE(2)
RefreshOnTop        EQUATE(3)
RefreshOnBottom     EQUATE(4)
RefreshOnCurrent    EQUATE(5)
  CODE
#IF(%HelpFile)
  HELP('%HelpFile')
#ENDIF
  #EMBED(%ProgramSetup,'Program Setup')
  %FirstProcedure
  #EMBED(%ProgramEnd,'Program End')
!---------------------------------------------------------------------------
#EMBED(%ProgramRoutines,'Program Routines'),DATA

